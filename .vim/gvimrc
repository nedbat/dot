" Ned's .gvimrc file

set guioptions-=T                       " No toolbar
set guioptions-=m                       " No menubar
set guioptions-=R                       " No scrollbars on right for vsplit windows
set guioptions-=l                       " No scrollbars on left
set guioptions-=L                       " No scrollbars on left
set guioptions-=b                       " No scrollbars on bottom

" Prevent the mouse from popping up a menu.
set mousemodel=extend

set visualbell
set t_vb=

colorscheme neds                        " Use my colors to whatever extent possible.

if has("mac")
    set macmeta
endif

" Set the normal font face and size.
function! FontNormal()
    if has('win32') || has('win64') || has('mac')
        set guifont=Consolas:h13
        set printfont=Consolas:h9
    else
        set guifont=Consolas\ 13
        set printfont=Consolas\ 9
    endif
    set linespace=0
    set lines=999 columns=999           " Maximize the window
endfunction

" Increase/decrease font size.
function! FontSmaller()
    if has('win32') || has('win64') || has('mac')
        let &guifont = substitute(&guifont, ':h\(\d\+\)', '\=":h" . (submatch(1) - 2)', '')
    else
        let &guifont = substitute(&guifont, ' \(\d\+\)', '\=" " . (submatch(1) - 2)', '')
    endif
    set lines=999 columns=999
endfunction

function! FontBigger()
    if has('win32') || has('win64') || has('mac')
        let &guifont = substitute(&guifont, ':h\(\d\+\)', '\=":h" . (submatch(1) + 2)', '')
    else
        let &guifont = substitute(&guifont, ' \(\d\+\)', '\=" " . (submatch(1) + 2)', '')
    endif
endfunction

nnoremap <Leader>0 :call FontNormal()<cr>
nnoremap <Leader>- :call FontSmaller()<cr>
nnoremap <Leader>+ :call FontBigger()<cr>
xnoremap <Leader>0 :call FontNormal()<cr>
xnoremap <Leader>- :call FontSmaller()<cr>
xnoremap <Leader>+ :call FontBigger()<cr>

call FontNormal()

" vim: tabstop=4 softtabstop=4 shiftwidth=4 expandtab
